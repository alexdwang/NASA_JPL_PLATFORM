* Replace content surrounded by {}, including '{}' signs, with your circuit file content. Do not modify or delete anything else including comments
Title: model / TL431 / Vref / T= 300.15K = 27C

*Input Voltage Source
*********************
V1 1 0 DC 0V
*End Input Voltage Source

*Circuit Core
*************
*Subcircuit
*X1 REF K A
X1 2 3 0 TL431_sc

*Resistance: R<name> <+ node> <- node> [model name] <value>
r21 4 0 6800
r22 5 4 10000
r23 5 1 180

Vika 5 3 0
Viref 4 2 0
*End Circuit Core

*Input
******
.dc V1 10 25 0.1
*End Input

*Output
*******
.print dc format=noindex file=TL431_Prerad_Test_Yidi.txt
+ V(1)
+ V(2)
*End Output

*Subcircuit
************
.subckt TL431_sc REF K A 
*Capacitance: C<name> <+ node> <- node> [model name] <value> + [IC=<initial value>]
C1 5 3 20p
C2 16 K 20p

*Resistance: R<name> <+ node> <- node> [model name] <value>
R1 K 13 180
R20 K 12 180
R2 6 5 1100
R5 4 3 2400
R4 4 1 2400
R3 7 4 2500
R8 19 A 4800
R9 19 18 33
R7 8 1 270
R6 9 A 373

*BJT: Q<name> <collector> <base> <emitter> [substrate] <model name> [area value]
Q2 K REF 7 QNMOD 2
Q3 16 16 REF QNMOD 3
Q4 11 11 12 QLPMOD 2
Q5 16 11 13 QLPMOD 1
Q6 11 7 6 QNMOD 1
Q7 3 1 9 QNMOD 7.2
Q8 1 1 A QNMOD 0.9
Q9 5 3 A QNMOD 1
Q10 16 8 A QNMOD 1
Q11 K 16 18 QNMOD 2.5
Q12 K 19 A QNMOD 50

*ends of the TL431_sc subcircuit
.ends
*End Subcircuit

*Library
*******
* npn prerad off ctp 3b
.model QNMOD NPN (              
+ IS = 1.68208E-16
+ BF = 84.058    NF = 0.986787 VAF = 351.9861415
+ IKF = 9.86E-3  NK = 0.47574  ISE = 7.1029E-15
+ NE = 2.06453   BR = 0.697    NR = 2
+ VAR = 100      IKR = 0.1     ISC = 1E-17
+ NC = 2         RB = 140.86   IRB = 1E-3
+ RBM = 50       RE = 2        RC = 250.75)

*lpnp prerad off ctp 3b
.model QLPMOD PNP (             
+ IS = 8.70964E-16
+ BF = 786.9		NF = 0.99                           VAF = 36.3423711
+ IKF = 6.30957E-5       NK = 0.52                           ISE = 9.54993E-17
+ NE = 1.27089           BR = 0.697                          NR = 2
+ VAR = 100              IKR = 0.1                           ISC = 1E-17
+ NC = 2                 RB = 758.578                        IRB = 3.6E-5
+ RBM = 100              RE = 4.096                           RC = 1)
*End Library

*end of the netlist
.end
